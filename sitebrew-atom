#!/usr/bin/env perl
use v5.14;
use FindBin;
use lib "$FindBin::Bin/lib";

use File::Find;
use utf8;
use IO::All;
use YAML;
use DateTimeX::Easy;
use XML::Feed;

use Sitebrew;
use Sitebrew::Article;

Sitebrew->initialize(app_root => io->curdir->absolute->name);

my $brewer = Sitebrew->instance;

my @articles = grep { defined } (sort { $b->published_at <=> $a->published_at } Sitebrew::Article->all)[0..24];

my $feed = XML::Feed->new("Atom", version => 1.0);

$feed->id($brewer->config->url_base->canonical->as_string);
$feed->title($brewer->config->title);
$feed->link($brewer->config->url_base);
$feed->self_link($brewer->config->url_base . "/atom.xml");
$feed->modified(DateTime->now);

for my $article (@articles) {
    next if $article->content_file eq 'content/index.md';
    $feed->add_entry(do {
        my $x = XML::Feed::Entry->new;
        $x->id($brewer->config->url_base . $article->href . '?' . $article->content_digest);
        $x->link($brewer->config->url_base . $article->href);
        $x->title($article->title);
        $x->modified($article->published_at);
        $x->author($ENV{USER});
        $x->summary( $article->summary );

        $x;
    });
}

io("public/atom.xml")->print( $feed->as_xml );

say "DONE: public/atom.xml";

