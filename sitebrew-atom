#!/usr/bin/env perl
use v5.14;
use FindBin;
use lib "$FindBin::Bin/lib";

use File::Find;
use utf8;
use IO::All;
use YAML;
use DateTimeX::Easy;
use XML::Feed;

use Sitebrew;

my $brewer = Sitebrew->new(app_root => io->curdir->absolute->name);

my @articles;

find({
    wanted => sub {
        return unless -f $_ && /attributes\.yml/;

        my $content_file = $_ =~ s/attributes\.yml$/index.md/r;

        my $href = $_ =~ s/attributes\.yml$//r =~ s{^content/}{}r;

        my $attrs = YAML::LoadFile($_);
        $attrs->{DATE} = DateTimeX::Easy->parse_datetime($attrs->{DATE});
        $attrs->{href} = $href;

        $attrs->{title} = io($content_file)->getline =~ s/^# //r =~ s/\n$//sr;

        push @articles, $attrs;
    },
    no_chdir => 1
}, 'content');

@articles = sort { $b->{DATE} <=> $a->{DATE} } @articles;

my $feed = XML::Feed->new("Atom", version => 1.0);

$feed->title($brewer->config->title);
$feed->link($brewer->config->url_base);
$feed->self_link($brewer->config->url_base . "/atom.xml");
$feed->modified(DateTime->now);

for my $article (@articles) {
    $feed->add_entry(do {
        my $x = XML::Feed::Entry->new;
        $x->link($article->{href});
        $x->title($article->{title});
        $x->modified($x->{DATE});
        $x->author($ENV{USER});

        $x;
    });
}

io("public/atom.xml")->print( $feed->as_xml );

say "DONE: public/atom.xml";

